apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "maven-publish"
apply plugin: "io.freefair.android-javadoc-jar"
apply plugin: "io.freefair.android-sources-jar"

android {
    namespace 'com.parse.google'
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
            buildConfigField("String","PARSE_VERSION","\"${version}\"")
        }
        release {
            minifyEnabled false
            testCoverageEnabled = false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            buildConfigField("String","PARSE_VERSION","\"${version}\"")
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api "com.google.android.gms:play-services-auth:20.2.0"
    api "androidx.activity:activity-ktx:1.5.1"
    api "androidx.fragment:fragment-ktx:1.5.2"
    implementation project(":parse")
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.findByName('release')
            }
        }
    }
}
