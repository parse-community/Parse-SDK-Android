apply plugin: 'com.android.library'
apply plugin: 'com.github.kt3k.coveralls'

group = 'com.parse'
version = rootProject.ext.commonLibVersion

ext {
    projDescription = 'A library that gives you access to the powerful Parse cloud platform from your Android app.'
    artifact = 'parse-android'
    projName = 'Parse-Android'
    gitLink = 'https://github.com/parse-community/Parse-SDK-Android'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName project.version
        consumerProguardFiles 'release-proguard.pro'
    }

    packagingOptions {
        exclude '**/BuildConfig.class'
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }
}

ext {
    okhttpVersion = '3.10.0'
}

dependencies {
    api "com.android.support:support-annotations:$supportLibVersion"
    api 'com.parse.bolts:bolts-tasks:1.4.0'
    api "com.squareup.okhttp3:okhttp:$okhttpVersion"

    testImplementation 'org.robolectric:robolectric:3.3.2'
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
}

//region Code Coverage

apply plugin: 'jacoco'

jacoco {
    toolVersion "0.7.1.201405082137"
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"

    classDirectories = fileTree(
            dir: "${buildDir}/intermediates/classes/debug",
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewInjector*.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*']
    )

    sourceDirectories = files("${buildDir.parent}/src/main/java")
    additionalSourceDirs = files([
            "${buildDir}/generated/source/buildConfig/debug",
            "${buildDir}/generated/source/r/debug"
    ])
    executionData = files("${buildDir}/jacoco/testDebugUnitTest.exec")

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

//endregion

//region Coveralls

coveralls.jacocoReportPath = "${buildDir}/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"

//endregion

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
