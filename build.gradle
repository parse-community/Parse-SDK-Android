buildscript {
    ext.kotlin_version = "1.5.31"
    ext.jacocoVersion = '0.8.7'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.2.2"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.3"
        classpath "com.github.dcendents:android-maven-gradle-plugin:2.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jacoco:org.jacoco.core:$jacocoVersion"
        classpath "com.dicedmelon.gradle:jacoco-android:0.1.5"
        classpath "io.freefair.gradle:android-gradle-plugins:4.2.0-m1"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:5.16.0"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.28.0"
    id "com.diffplug.spotless" version "5.16.0"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    apply plugin: "com.diffplug.spotless"

    spotless {
        java {
            target '**/*.java'
            googleJavaFormat().aosp()
            removeUnusedImports()
            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
        kotlin {
            target '**/*.kt'
            ktlint().userData(["disabled_rules": "no-wildcard-imports"])
            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
        format 'misc', {
            target '**/*.gradle', '**/*.md', '**/.gitignore'
            indentWithSpaces()
            trimTrailingWhitespace()
            endWithNewline()
        }

        format 'xml', {
            target '**/*.xml'
            indentWithSpaces()
            trimTrailingWhitespace()
            endWithNewline()
        }
    }
}

ext {
    compileSdkVersion = 30

    minSdkVersion = 16
    targetSdkVersion = 30
}