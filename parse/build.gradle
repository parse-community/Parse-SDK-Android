apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        consumerProguardFiles "release-proguard.pro"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    packagingOptions {
        exclude '**/BuildConfig.class'
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }
}

ext {
    // Note: Don't update past 3.12.x, as it sets the minSdk to Android 5.0
    okhttpVersion = "3.12.10"
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api "androidx.annotation:annotation:1.2.0"
    api "androidx.core:core-ktx:1.6.0"
    api "com.squareup.okhttp3:okhttp:$okhttpVersion"
    api project(':bolts-tasks')

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.robolectric:robolectric:4.6"
    testImplementation "org.skyscreamer:jsonassert:1.5.0"
    testImplementation "org.mockito:mockito-core:3.6.28"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
}

//region Code Coverage

apply plugin: "com.dicedmelon.gradle.jacoco-android"

jacoco {
    toolVersion = "0.8.6"
}

jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true
}

//endregion

//region Coveralls

apply plugin: "com.github.kt3k.coveralls"

coveralls.jacocoReportPath = "${buildDir}/jacoco/jacoco.xml"

//endregion

apply from: "https://raw.githubusercontent.com/Commit451/gradle-android-javadocs/2.0.0/gradle-android-javadocs.gradle"
